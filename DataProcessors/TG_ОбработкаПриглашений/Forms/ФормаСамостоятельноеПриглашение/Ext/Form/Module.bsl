
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтотОбъект.Сотрудник = Пользователи.ТекущийПользователь();
	СотрудникУжеЗарегистрирован = ЕстьРегистрация(ЭтотОбъект.Сотрудник);
	
	Если СотрудникУжеЗарегистрирован Тогда
		Элементы.ДекорацияСвзяьУжеСуществует.Видимость = Истина;
		Элементы.ФормаПолучитьПриглашение.Видимость = Ложь;
		Элементы.СсылкаПриглашения.Видимость = Ложь;
		Возврат;
	КонецЕсли; 
	
	СуществующееПриглашение = ЕстьСтароеПриглашение();
	
	Если СуществующееПриглашение <> Неопределено Тогда
		ЭтотОбъект.СсылкаПриглашения = СуществующееПриглашение;
		Элементы.ФормаПолучитьПриглашение.Видимость = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьПриглашение(Команда)
	
	ДанныеБота = ОсновнойБот();	
	IDПриглашения = Новый УникальныйИдентификатор;	
	СсылкаПриглашения = СтрШаблон("%1?start=%2", ДанныеБота.СсылкаНаБота, IDПриглашения); 
	ТекущийСотрудник = ЭтотОбъект.Сотрудник;
	Попытка
		СохранитьПриглашение(ТекущийСотрудник, ДанныеБота.Бот, IDПриглашения, СсылкаПриглашения);
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ПоказатьПредупреждение(,НСтр("ru = 'Сохранение нового приглашения не было выполнено по причине:'") 
		+ Символы.ПС + ТекстСообщения);
		Возврат;
	КонецПопытки;
	ЭтотОбъект.СсылкаПриглашения = СсылкаПриглашения;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЕстьСтароеПриглашение()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	TG_Приглашения.СсылкаПриглашения КАК СсылкаПриглашения
	|ИЗ
	|	РегистрСведений.TG_Приглашения КАК TG_Приглашения
	|ГДЕ
	|	TG_Приглашения.Сотрудник = &Сотрудник";
	Запрос.УстановитьПараметр("Сотрудник", Пользователи.ТекущийПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.СсылкаПриглашения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОсновнойБот()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	TG_Боты.СсылкаНаБота КАК СсылкаНаБота,
	|	TG_Боты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.TG_Боты КАК TG_Боты
	|ГДЕ
	|	TG_Боты.Основной = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ДанныеБота = Новый Структура;
	ДанныеБота.Вставить("СсылкаНаБота", Выборка.СсылкаНаБота);
	ДанныеБота.Вставить("Бот", Выборка.Ссылка);
	
	Возврат ДанныеБота;
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьПриглашение(Сотрудник, Бот, IDПриглашения, СсылкаПриглашения)
	
	НаборЗаписей = РегистрыСведений.TG_Приглашения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сотрудник.Значение = Сотрудник;
	НаборЗаписей.Отбор.Бот.Значение = Бот;
	НаборЗаписей.Отбор.Сотрудник.Использование = Истина;
	НаборЗаписей.Отбор.Бот.Использование = Истина;
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.IDПриглашения = IDПриглашения;
	НоваяЗапись.Сотрудник = Сотрудник;
	НоваяЗапись.Бот = Бот;
	НоваяЗапись.СсылкаПриглашения = СсылкаПриглашения;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Сохранение нового приглашения'"),
		УровеньЖурналаРегистрации.Ошибка,,,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));    
		
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьРегистрация(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	TG_СоответствияСотрудниковID.ТелеграмId КАК ТелеграмId
	|ИЗ
	|	РегистрСведений.TG_СоответствияСотрудниковID КАК TG_СоответствияСотрудниковID
	|ГДЕ
	|	TG_СоответствияСотрудниковID.Сотрудник = &Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
КонецФункции
 
#КонецОбласти