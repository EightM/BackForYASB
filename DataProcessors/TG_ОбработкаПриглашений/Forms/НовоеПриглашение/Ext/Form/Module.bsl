
#Область ОбработчикиСобытийФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура АдресБотаПриИзмененииНаСервере()
	ЭтотОбъект.СсылкаНаБота = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.Бот, "СсылкаНаБота");
КонецПроцедуры

&НаКлиенте
Процедура АдресБотаПриИзменении(Элемент)
	АдресБотаПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьСсылку(Команда)
	
	ЕстьСтароеПриглашение = ПриглашениеСуществует(ЭтотОбъект.Сотрудник);
	Если ЕстьСтароеПриглашение Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для указанного сотрудника уже имеется активное приглашение");
		Возврат;
	КонецЕсли; 
	
	IDПриглашения = Новый УникальныйИдентификатор;	
	СсылкаПриглашения = СтрШаблон("%1?start=%2", ЭтотОбъект.СсылкаНаБота, IDПриглашения); 
	Попытка
		СохранитьПриглашение(ЭтотОбъект.Сотрудник, ЭтотОбъект.Бот, IDПриглашения, СсылкаПриглашения);
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ПоказатьПредупреждение(,НСтр("ru = 'Сохранение нового приглашения не было выполнено по причине:'") 
		+ Символы.ПС + ТекстСообщения);
		Возврат;
	КонецПопытки;
	ЭтотОбъект.СсылкаПриглашения = СсылкаПриглашения; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьПриглашение(Сотрудник, Бот, IDПриглашения, СсылкаПриглашения)
	НаборЗаписей = РегистрыСведений.TG_Приглашения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сотрудник.Значение = Сотрудник;
	НаборЗаписей.Отбор.Бот.Значение = Бот;
	НаборЗаписей.Отбор.Сотрудник.Использование = Истина;
	НаборЗаписей.Отбор.Бот.Использование = Истина;
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.IDПриглашения = IDПриглашения;
	НоваяЗапись.Сотрудник = Сотрудник;
	НоваяЗапись.Бот = Бот;
	НоваяЗапись.СсылкаПриглашения = СсылкаПриглашения;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Сохранение нового приглашения'"),
		УровеньЖурналаРегистрации.Ошибка,,,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));    
		
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПриглашениеСуществует(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	TG_Приглашения.IDПриглашения КАК IDПриглашения
	|ИЗ
	|	РегистрСведений.TG_Приглашения КАК TG_Приглашения
	|ГДЕ
	|	TG_Приглашения.Сотрудник = &Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции
 
#КонецОбласти
